from snakemake.utils import min_version
from pathlib import Path
import pandas as pd

##### set minimum snakemake version #####
min_version("8.4.1")


configfile: "config/config.yaml"


include: "rules/common.smk"
include: "rules/delivery.smk"


rule all:
    input:
        expand(dst_path("{seqid}/.delivery.done"), seqid=matching_map().seqid),
        dst_path("tables_delivery.done"),
        expand(dst_path("plots/{seqid}.png"), seqid=matching_map().seqid),


rule create_profiles:
    input:
        template=config.get("template_profile"),
    params:
        outdir=ws_path("metal/{seqid}"),
        matching_map=matching_map(),
    output:
        ws_path("metal/{seqid}/profile_{seqid}.txt"),
    conda:
        "envs/python.yml"
    resources:
        runtime=lambda wc, attempt: attempt * 15,
    script:
        "scripts/create_profiles.py"


rule execute_metal:
    input:
        ws_path("metal/{seqid}/profile_{seqid}.txt"),
    output:
        ws_path("metal/{seqid}/METAL_output_{seqid}_1.TBL"),
    container:
        "docker://gitlab.fht.org:5050/hds-center/containers/metal:c9558b23"
    resources:
        runtime=lambda wc, attempt: attempt * 10,
    shell:
        "metal {input}"


rule sort:
    input:
        ws_path("metal/{seqid}/METAL_output_{seqid}_1.TBL"),
    output:
        ws_path("metal/{seqid}/METAL_output_{seqid}_1.sorted.TBL"),
    resources:
        runtime=lambda wc, attempt: attempt * 60,
    shell:
        "sort --parallel={threads} -k 1n -k 2n {input} > {output}"


rule filter:
    input:
        ws_path("metal/{seqid}/METAL_output_{seqid}_1.sorted.TBL"),
    output:
        ws_path("metal/{seqid}/METAL_output_{seqid}_1.sorted.filtered.TBL"),
    resources:
        runtime=lambda wc, attempt: attempt * 60,
    params:
        char="?",
    shell:
        """
        grep -v {params.char} {input} > {output}
        """


rule standardize:
    input:
        ws_path("metal/{seqid}/METAL_output_{seqid}_1.sorted.filtered.TBL"),
    output:
        ws_path("metal/{seqid}/{seqid}.metal_het.tsv.gz"),
        temp(ws_path("min_P/{seqid}.nlargest.txt")),
        temp(ws_path("if/{seqid}.if.txt")),
        ws_path("plots/{seqid}.png"),
    conda:
        "scripts/gwaspipe/environment.yml"
    params:
        format=config.get("params").get("standardize_sumstats").get("input_format"),
        config_file=config.get("params").get("standardize_sumstats").get("config_file"),
        output_path=config.get("workspace_path"),
    resources:
        runtime=lambda wc, attempt: attempt * 30,
    shell:
        "python workflow/scripts/gwaspipe/src/gwaspipe.py "
        "-f {params.format} "
        "-c {params.config_file} "
        "-i {input} "
        "-o {params.output_path}"


rule generate_if_table:
    input:
        expand(ws_path("if/{seqid}.if.txt"), seqid=matching_map().seqid),
    output:
        ws_path("inflation_factors_table.tsv"),
    conda:
        "envs/create_report_table.yaml"
    params:
        input_path=ws_path("if"),
    shell:
        "python workflow/scripts/create_report_table.py -i {params.input_path} -o {output}"


rule generate_min_pvalue_table:
    input:
        expand(ws_path("min_P/{seqid}.nlargest.txt"), seqid=matching_map().seqid),
    output:
        ws_path("min_pvalue_table.tsv"),
    conda:
        "envs/create_report_table.yaml"
    params:
        input_path=ws_path("min_P"),
    shell:
        "python workflow/scripts/create_report_table.py -i {params.input_path} -o {output}"
